plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.otto.beluga'
version = '0.0.1'

java {
    sourceCompatibility = '17'
}

jacoco {
    toolVersion = '0.8.7'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    cucumberRuntime {
        extendsFrom testImplementation
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.cucumber:cucumber-spring:7.13.0'
    implementation 'io.cucumber:cucumber-java:7.13.0'

    implementation 'org.springdoc:springdoc-openapi-ui:1.6.12'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:4.4.0'
    testImplementation 'io.rest-assured:rest-assured-common:4.4.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('cucumberCli') {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'com.otto.beluga',
                    'src/test/resources']
        }
    }
}

test {
    finalizedBy jacocoTestReport
}